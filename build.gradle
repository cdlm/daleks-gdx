allprojects {
    version = '1.0'

    ext {
        appName = 'Daleks'
        gdxVersion = '1.9.8'
    }

    apply plugin: 'java'
    repositories { jcenter() }

    apply plugin: 'idea'
    idea.module {
        downloadJavadoc = true
        downloadSources = true
    }
}

task atlases(type:JavaExec) {
    classpath file("${rootDir}/buildSrc/build/classes/java/main")
    main = 'de.nxg.kenneytolibgdxatlas.KenneyToLibGdxAtlasConverter'
    args 'core/src/main/resources'
}

project(':core') {
    dependencies {
        compile group: 'com.badlogicgames.gdx', name: 'gdx', version: rootProject.gdxVersion
    }
}

project(':desktop') {
    dependencies {
        compile project(':core')
        compile group: 'com.badlogicgames.gdx', name: 'gdx-backend-lwjgl', version: rootProject.gdxVersion
        compile group: 'com.badlogicgames.gdx', name: 'gdx-platform', version: rootProject.gdxVersion, classifier: 'natives-desktop'
    }

    apply plugin: 'application'
    mainClassName = 'imt.uvinfo.daleks.desktop.DaleksDesktop'
    run.dependsOn atlases
}
